# Maintainer: Jonathan Raffre (nekonyuu) <nk@nyuu.eu>
# Contributor (based on): Bjorn Neergaard (neersighted) <bjorn@neersighted.com>
# Contributor: JD Horelick <jdhore1@gmail.com>
# Contributor: Mantas MikulÄ—nas <grawity@gmail.com>
# Contributor: Avi H.D. <strykar@hotmail.com>

_pkgname=charybdis
pkgname=charybdis
pkgver=4.1.2
pkgrel=1
pkgdesc='A higly scalable IRCv3.2 server for large, community-oriented networks'
url='https://github.com/charybdis-ircd/'
license=('GPL2')
options=('!strip')
install=charybdis.install
source=("https://github.com/charybdis-ircd/charybdis/archive/${pkgname}-4.1.2.tar.gz"
        'charybdis.service'
        'charybdis.tmpfiles')
sha256sums=('a3f4e572877c5a3abdab3af652746c0682f0c41b3f9e3c9866315f5d03f2bb57'
            '044f3567ed865365e76581942e1b28ba4f7193dd6c8022616ea0894295272062'
            '2fcc284f060cafa9a6df0b010de44833b248169ab11c6289f54227ab87068c29')
arch=('i686' 'x86_64' 'aarch64' 'armv7h')
depends=('bison' 'flex' 'openssl' 'zlib')
optdepends=('atheme: IRC services like Nickserv and Chanserv')
provides=('charybdis')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgname}-${pkgver}"
  ./autogen.sh
}

build() {
  cd "${srcdir}/${pkgname}-${pkgname}-${pkgver}"

  # Configure the build.
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc/charybdis \
    --libexecdir=/usr/lib/charybdis \
    --localstatedir=/var \
    --enable-fhs-paths \
    --with-rundir=/run \
    --with-program-prefix=charybdis-
    
  # Build it!
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgname}-${pkgver}"

  # Install the program.
  make DESTDIR="${pkgdir}" install

  # Install the docs.
  install -Dm644 -d docs "${pkgdir}/usr/share/charybdis"

  # Install the service.
  install -Dm644 "${srcdir}/charybdis.service" "${pkgdir}/usr/lib/systemd/system/charybdis.service"
  install -Dm644 "${srcdir}/charybdis.tmpfiles" "${pkgdir}/usr/lib/tmpfiles.d/charybdis.conf"
}

# vim: ft=sh ts=2 sw=2 et
